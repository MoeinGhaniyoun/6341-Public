package parser;

import java.util.*;
import java.io.*;
import java_cup.runtime.Symbol;
import java_cup.runtime.ComplexSymbolFactory.ComplexSymbol;
import java_cup.runtime.ComplexSymbolFactory;

import ast.*;
import interpreter.Interpreter;

parser code {: 
  Lexer lexer;

  public Parser(Lexer lex, ComplexSymbolFactory sf) {
    super(lex, sf);
    lexer = lex;
  }

  public void report_fatal_error(String message, Object info) {
    Interpreter.fatalError(message + " (" + info + ")", Interpreter.EXIT_PARSING_ERROR);
  }

  Location loc(int line, int column) {
    return new Location(line, column);
  }

:};

terminal INT, REF, QTYPE, MUTABLE, NIL,
         IF, ELSE, WHILE, RETURN,
         SEMICOLON, COMMA, LPAREN, RPAREN, BEGIN, END,
         ASSIGN, PLUS, MINUS, UMINUS, TIMES, DOT,
         LE, GE, EQ, NE, LT, GT, AND, OR, NOT;
terminal Long    INTCONST;
terminal String  IDENT;

non terminal Program         program;
non terminal List<FuncDef>   funcDefList;
non terminal FuncDef         funcDef;
non terminal VarDecl         varDecl;
non terminal Type            type;
non terminal List<VarDecl>   formalDeclList;
non terminal List<VarDecl>   neFormalDeclList;
non terminal List<Stmt>      stmtList;
non terminal Stmt            stmt;
non terminal List<Expr>      exprList;
non terminal List<Expr>      neExprList;
non terminal Expr            expr;
non terminal BinaryExpr      binaryExpr;
non terminal Cond            cond;

precedence left DOT;
precedence left PLUS, MINUS, AND, OR;
precedence left TIMES;
precedence left UMINUS, NOT;

precedence left ELSE; // resolves dangling else

start with program;

program ::= funcDefList:l
    {: RESULT = new Program(l, loc(lleft, lright)); :}
    ;

funcDefList ::= funcDef:d funcDefList:l
    {: l.add(0, d); RESULT = l; :}
    | /* empty list */
    {: RESULT = new LinkedList<FuncDef>(); :}
    ;

funcDef ::= varDecl:d LPAREN formalDeclList:dl RPAREN BEGIN stmtList:sl END
    {: RESULT = new FuncDef(d, dl, sl, loc(dleft, dright)); :}
    ;

varDecl ::= type:t IDENT:i
    {: RESULT = new VarDecl(false, t, i, loc(tleft, tright)); :}
    | MUTABLE:m type:t IDENT:i
    {: RESULT = new VarDecl(true, t, i, loc(mleft, mright)); :}
    ;

type ::= INT
    {: RESULT = Type.INT; :}
    | REF
    {: RESULT = Type.REF; :}
    | QTYPE
    {: RESULT = Type.QTYPE; :}
    ;

formalDeclList ::= neFormalDeclList:l
    {: RESULT = l; :}
    | /* empty list */
    {: RESULT = new LinkedList<VarDecl>(); :}
    ;

neFormalDeclList ::= varDecl:d COMMA neFormalDeclList:l
    {: l.add(0, d); RESULT = l; :}
    | varDecl:d
    {: RESULT = new LinkedList<VarDecl>(Arrays.asList(d)); :}
    ;

stmtList ::= stmt:s stmtList:l
    {: l.add(0, s); RESULT = l; :}
    | /* empty list */
    {: RESULT = new LinkedList<Stmt>(); :}
    ;

stmt ::= varDecl:d ASSIGN expr:e SEMICOLON
    {: RESULT = new DeclStmt(d, e, loc(dleft, dright)); :}
    | IDENT:i ASSIGN expr:e SEMICOLON
    {: RESULT = new AssignStmt(i, e, loc(ileft, iright)); :}
    | IF:i LPAREN cond:e RPAREN stmt:s
    {: RESULT = new IfStmt(e, s, loc(ileft, iright)); :}
    | IF:i LPAREN cond:e RPAREN stmt:s1 ELSE stmt:s2
    {: RESULT = new IfStmt(e, s1, s2, loc(ileft, iright)); :}
    | WHILE:w LPAREN cond:e RPAREN stmt:s
    {: RESULT = new WhileStmt(e, s, loc(wleft, wright)); :}
    | IDENT:i LPAREN exprList:l RPAREN SEMICOLON
    {: RESULT = new CallStmt(new CallExpr(i, l, loc(ileft, iright))); :}
    | RETURN:r expr:e SEMICOLON
    {: RESULT = new ReturnStmt(e, loc(rleft, rright)); :}
    | BEGIN:b stmtList:l END
    {: RESULT = new CompoundStmt(l, loc(bleft, bright)); :}
    ;

exprList ::= neExprList:l
    {: RESULT = l; :}
    | /* empty list */
    {: RESULT = new LinkedList<Expr>(); :}
    ;

neExprList ::= expr:e COMMA neExprList:l
    {: l.add(0, e); RESULT = l; :}
    | expr:e
    {: RESULT = new LinkedList<Expr>(Arrays.asList(e)); :}
    ;

expr ::= NIL:n
    {: RESULT = new ConstExpr(loc(nleft, nright)); :}
    | INTCONST:c
    {: RESULT = new ConstExpr(c, loc(cleft, cright)); :}
    | IDENT:i
    {: RESULT = new IdentExpr(i, loc(ileft, iright)); :}
    | MINUS:m expr:e
    {: RESULT = new UnaryMinusExpr(e, loc(mleft, mright)); :}
    %prec UMINUS
    | LPAREN:l type:t RPAREN expr:e
    {: RESULT = new CastExpr(t, e, loc(lleft, lright)); :}
    %prec UMINUS
    | IDENT:i LPAREN exprList:l RPAREN
    {: RESULT = new CallExpr(i, l, loc(ileft, iright)); :}
    | binaryExpr:e
    {: RESULT = e; :}
    | LPAREN expr:e RPAREN
    {: RESULT = e; :}
    ;

binaryExpr ::= expr:e1 PLUS expr:e2
               {: RESULT = new BinaryExpr(e1, BinaryExpr.PLUS, e2, loc(e1left, e1right)); :}
            |  expr:e1 MINUS expr:e2
               {: RESULT = new BinaryExpr(e1, BinaryExpr.MINUS, e2, loc(e1left, e1right)); :}
            |  expr:e1 TIMES expr:e2
               {: RESULT = new BinaryExpr(e1, BinaryExpr.TIMES, e2, loc(e1left, e1right)); :}
            |  expr:e1 DOT expr:e2
               {: RESULT = new BinaryExpr(e1, BinaryExpr.DOT, e2, loc(e1left, e1right)); :}
            ;

cond ::= expr:e1 LE expr:e2
    {: RESULT = new CompCond(e1, CompCond.LE, e2, loc(e1left, e1right)); :}
    | expr:e1 GE expr:e2
    {: RESULT = new CompCond(e1, CompCond.GE, e2, loc(e1left, e1right)); :}
    | expr:e1 EQ expr:e2
    {: RESULT = new CompCond(e1, CompCond.EQ, e2, loc(e1left, e1right)); :}
    | expr:e1 NE expr:e2
    {: RESULT = new CompCond(e1, CompCond.NE, e2, loc(e1left, e1right)); :}
    | expr:e1 LT expr:e2
    {: RESULT = new CompCond(e1, CompCond.LT, e2, loc(e1left, e1right)); :}
    | expr:e1 GT expr:e2
    {: RESULT = new CompCond(e1, CompCond.GT, e2, loc(e1left, e1right)); :}
    | cond:e1 AND cond:e2
    {: RESULT = new LogicalCond(e1, LogicalCond.AND, e2, loc(e1left, e1right)); :}
    | cond:e1 OR cond:e2
    {: RESULT = new LogicalCond(e1, LogicalCond.OR, e2, loc(e1left, e1right)); :}
    | NOT:n cond:e
    {: RESULT = new NotExpr(e, loc(nleft, nright)); :}
    | LPAREN cond: e RPAREN
    {: RESULT = e; :}
    ;
